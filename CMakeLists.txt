cmake_minimum_required(VERSION 3.9)
project(FTengSignals
	VERSION 0.0.1
	LANGUAGES CXX
)
set(FTENG_SIGNALS_VERSION ${PROJECT_VERSION})

if(NOT DEFINED FTENG_SIGNALS_BUNDLED)
	if("${PROJECT_SOURCE_DIR}" STREQUAL "${CMAKE_SOURCE_DIR}")
		set(FTENG_SIGNALS_BUNDLED FALSE)
	else()
		set(FTENG_SIGNALS_BUNDLED TRUE)
	endif()
endif()
set(FTENG_SIGNALS_BUNDLED ${FTENG_SIGNALS_BUNDLED} CACHE BOOL "" FORCE)
mark_as_advanced(FTENG_SIGNALS_BUNDLED)

if(NOT FTENG_SIGNALS_BUNDLED)
	include(GNUInstallDirs)
	set(FTENG_SIGNALS_TARGETS "FTengSignalsTargets")
	set(FTENG_SIGNALS_BUILD_TEST_APP_DEFAULT TRUE)
	set(FTENG_SIGNALS_BUILD_BENCHMARKS_DEFAULT FALSE)
else()
	set(FTENG_SIGNALS_BUILD_TEST_APP_DEFAULT FALSE)
	set(FTENG_SIGNALS_BUILD_BENCHMARKS_DEFAULT FALSE)
endif()

option(FTENG_SIGNALS_BUILD_TEST_APP "Build test app" ${FTENG_SIGNALS_BUILD_TEST_APP_DEFAULT})
option(FTENG_SIGNALS_BUILD_BENCHMARKS "Build benchmarks" ${FTENG_SIGNALS_BUILD_BENCHMARKS_DEFAULT})

add_subdirectory(Signals)
if(FTENG_SIGNALS_BUILD_TEST_APP)
	add_subdirectory(TestApp)
endif()
if(FTENG_SIGNALS_BUILD_BENCHMARKS)
	add_subdirectory(Benchmarks)
endif()

if(NOT FTENG_SIGNALS_BUNDLED)
	include(CMakePackageConfigHelpers)
	set(FTENG_SIGNALS_VERSION_CONFIG ${PROJECT_BINARY_DIR}/FTengSignalsConfigVersion.cmake)
	set(FTENG_SIGNALS_PROJECT_CONFIG ${PROJECT_BINARY_DIR}/FTengSignalsConfig.cmake)
	set(FTENG_SIGNALS_TARGETS_CONFIG ${PROJECT_BINARY_DIR}/FTengSignalsTargets.cmake)
	set(FTENG_SIGNALS_CONFIG_INSTALL_DIR ${CMAKE_INSTALL_LIBDIR}/cmake/FTengSignals/)
	write_basic_package_version_file(
		${FTENG_SIGNALS_VERSION_CONFIG}
		VERSION ${FTENG_SIGNALS_VERSION}
		COMPATIBILITY SameMajorVersion
		ARCH_INDEPENDENT
	)
	configure_package_config_file(
		${CMAKE_CURRENT_LIST_DIR}/CMake/FTengSignalsConfig.cmake.in
		${FTENG_SIGNALS_PROJECT_CONFIG}
		INSTALL_DESTINATION ${FTENG_SIGNALS_CONFIG_INSTALL_DIR}
	)

	export(EXPORT ${FTENG_SIGNALS_TARGETS}
		FILE ${FTENG_SIGNALS_TARGETS_CONFIG}
		NAMESPACE FTengSignals::
	)
	install(EXPORT ${FTENG_SIGNALS_TARGETS}
		DESTINATION ${FTENG_SIGNALS_CONFIG_INSTALL_DIR}
		NAMESPACE FTengSignals::
	)
	install(
		FILES ${FTENG_SIGNALS_PROJECT_CONFIG} ${FTENG_SIGNALS_VERSION_CONFIG}
		DESTINATION ${FTENG_SIGNALS_CONFIG_INSTALL_DIR}
	)
endif()

